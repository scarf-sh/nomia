entrypoints Name;

comment "/*" "*/" ;

-- TODO Need unicode identifier support before finalizing
-- TODO Should all of the uses of Identifier really be the same rules?
token Identifier letter (letter | digit | '_' | '-' | '\'')* ;

LetIn. Name ::= "let" [Declaration] "in" Substitution ;
tree.  Name ::= Substitution ;
define tree s = LetIn [ ] s ;

Declare.  Declaration ::= Binding Substitution ;
separator Declaration ";" ;

Unbound. Binding ::= ;
Bound.   Binding ::= Identifier "=" ;

Substitute. Substitution ::= NameRef Inputs ;

Nullary.  Inputs ::= ;
Multiary. Inputs ::= "(" [SubstitutionSpec] ")" GraphIndex ;

DefaultIndex. GraphIndex ::= ;
Located.      GraphIndex ::= "@" GraphTraversal NestedTraversal ;

SingletonDefault.   GraphTraversal ::= ;
SingletonSpecified. GraphTraversal ::= GraphOffset ;
SnocTraversal.      GraphTraversal ::= GraphTraversal "." GraphOffset ;

NamedOffset.      GraphOffset ::= Identifier ;
PositionalOffset. GraphOffset ::= Integer ;

NoNest.  NestedTraversal ::= ;
Descend. NestedTraversal ::= "!" GraphTraversal ;

Spec. SubstitutionSpec ::= FormalBinding OutputRef ;
separator nonempty SubstitutionSpec "," ;

Positional.  FormalBinding ::= ;
BoundFormal. FormalBinding ::= Identifier "=" ;

Output. OutputRef ::= Substitution OutputIdentifier;

DefaultOutput.   OutputIdentifier ::= ;
SpecifiedOutput. OutputIdentifier ::= "." Identifier GraphIndex;

AtomicRef.     NameRef ::= AtomicName ;
Variable.      NameRef ::= "$" Identifier ;
Resolved.      NameRef ::= "@" Identifier ;
NestedNameRef. NameRef ::= "(" Name ")";

Atomic. AtomicName ::= Qualification ParameterizedId ;

Unqualified. Qualification ::= ;
Qualified.   Qualification ::= NamespaceId ":" ;

Builtin.        NamespaceId ::= ParameterizedId ;
NamedNamespace. NamespaceId ::= "[" OutputRef "]" ;

ParamId. ParameterizedId ::= Identifier Params ;

UnParameterized. Params ::= ;
Parameterized.   Params ::= "?" [Param] ;

-- TODO Support multiple types? Some kind of variables to reference inputs to the name?
Quoted. ParamValue ::= String ;
-- TODO We should support more robust unquoted strings, but annoying to get the lexer to behave
Unquoted. ParamValue ::= Identifier ;
Parameter. Param ::= Identifier "=" ParamValue ;
separator nonempty Param "&" ;

-- TODO Name metadata
-- TODO response files... or maybe that's a first-class name?
